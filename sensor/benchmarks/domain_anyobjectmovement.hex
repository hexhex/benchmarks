%
% sorts
%

% C+
%:- sorts
%  xcoo;
%  ycoo;
%  object;
%  robot.
%
%:- objects
%  1..maxx :: xcoo;
%  1..maxy :: ycoo;
%  a,b :: robot;
%  u,v :: object.

% instance specific sorts
xcoo(X) :- #int(X), 1 <= X, X <= MX, maxx(MX).
ycoo(Y) :- #int(Y), 1 <= Y, Y <= MY, maxy(MY).

% fluents

% C+
%:- constants
%  objectX(object) :: staticFluent(xcoo);
%  objectY(object) :: staticFluent(ycoo);
%  robotX(robot) :: inertialFluent(xcoo);
%  robotY(robot) :: inertialFluent(ycoo);
%  found(object) :: inertialFluent.

% time (generic)

step(S) :- #int(S), 0 <= S, S <= LS, laststep(LS).
astep(S) :- #int(S), 0 <= S, S < LS, laststep(LS).
nextstep(S,SPrime) :- astep(S), SPrime = S + 1.

% generate all five possible moves
move(R,X,Y1,T) v nmove(R,X,Y1,T) :- robotX(R,X,T), robotY(R,Y,T), astep(T), Y1=Y+1, ycoo(Y1).
move(R,X,Y1,T) v nmove(R,X,Y1,T) :- robotX(R,X,T), robotY(R,Y,T), astep(T), Y1=Y-1, ycoo(Y1).
move(R,X1,Y,T) v nmove(R,X1,Y,T) :- robotX(R,X,T), robotY(R,Y,T), astep(T), X1=X+1, xcoo(X1).
move(R,X1,Y,T) v nmove(R,X1,Y,T) :- robotX(R,X,T), robotY(R,Y,T), astep(T), X1=X-1, xcoo(X1).
move(R,X,Y,T) v nmove(R,X,Y,T) :- robotX(R,X,T), robotY(R,Y,T), astep(T). % stay
% disallow moving to multiple places at once
:- move(R,X1,Y1,T), move(R,X2,Y2,T), X1 < X2.
:- move(R,X,Y1,T), move(R,X,Y2,T), Y1 < Y2.
% disallow not doing any action
moveexists(R,T) :- move(R,X,Y,T).
:- astep(T), robot(R), not moveexists(R,T).

% direct effect of move action
robotX(R,X,TPlus1) :- move(R,X,Y,T), nextstep(T,TPlus1).
robotY(R,Y,TPlus1) :- move(R,X,Y,T), nextstep(T,TPlus1).

%%%% saturation checking part (the following commented block is our co-np check)
%%%
%%%% random initial situation for object position
%%%objectX(O,X) v -objectX(O,X) :- object(O), xcoo(X).
%%%objectY(O,Y) v -objectY(O,Y) :- object(O), ycoo(Y).
%%%% disallow being in multiple places at once
%%%:- objectX(O,X1), objectX(O,X2), X1 < X2.
%%%:- objectY(O,Y1), objectY(O,Y2), Y1 < Y2.
%%%% disallow being nowhere
%%%objectexists(O) :- objectX(O,X), objectY(O,Y).
%%%:- object(O), not objectexists(O).
%%%
%%%% ramification of moving
%%%found(O) :- &senseObject[robotX,robotY,objectX,objectY,R](O), sensorrange(R).
%%%:- object(O), not found(O).

% saturation encoding (see Eiter Polleres "Automated Integration of Guess and Check ...")

% order over objects
before(O) :- object(O), object(O2), O2 < O.
after(O) :- object(O), object(O2), O2 > O.
between(O1,O2) :- object(O1), object(O2), object(O), O1 < O, O < O2.
first(O) :- object(O), not before(O).
last(O) :- object(O), not after(O).
next(O1,O2) :- object(O1), object(O2), O1 < O2, not between(O1,O2).

% guess of object movements (over which we will have head cycles)
objectmove(R,X,Y1,T) v nobjectmove(R,X,Y1,T) :- objectX(R,X,T), objectY(R,Y,T), astep(T), Y1=Y+1, ycoo(Y1).
objectmove(R,X,Y1,T) v nobjectmove(R,X,Y1,T) :- objectX(R,X,T), objectY(R,Y,T), astep(T), Y1=Y-1, ycoo(Y1).
objectmove(R,X1,Y,T) v nobjectmove(R,X1,Y,T) :- objectX(R,X,T), objectY(R,Y,T), astep(T), X1=X+1, xcoo(X1).
objectmove(R,X1,Y,T) v nobjectmove(R,X1,Y,T) :- objectX(R,X,T), objectY(R,Y,T), astep(T), X1=X-1, xcoo(X1).

% direct effect of object movement
objectX(R,X,TPlus1) :- objectmove(R,X,Y,T), nextstep(T,TPlus1).
objectY(R,Y,TPlus1) :- objectmove(R,X,Y,T), nextstep(T,TPlus1).

% disallow moving to multiple places at once
spoil :- objectX(O,X1,S), objectX(O,X2,S), X1 < X2.
spoil :- objectY(O,Y1,S), objectY(O,Y2,S), Y1 < Y2.

% disallow not moving for object
spoil :- nobjectmove(O,X1,Y2,S), nobjectmove(O,X2,Y1,S), nobjectmove(O,X2,Y3,S), nobjectmove(O,X3,Y2,S), Y1 < Y2, Y2 < Y3, X1 < X2, X2 < X3.

% disallow object is not sensed
objectsOkUpTo(O) :- first(O), &senseObjectVL[robotX,robotY,objectX,objectY,R,spoil,object](O), sensorrange(R).
objectsOkUpTo(O) :- objectsOkUpTo(OPrev), next(OPrev,O), &senseObjectVL[robotX,robotY,objectX,objectY,R,spoil,object](O), sensorrange(R).
spoil :- objectsOkUpTo(O), last(O).

objectX(O,X,S) :- object(O), xcoo(X), step(S), spoil.
objectY(O,Y,S) :- object(O), ycoo(Y), step(S), spoil.
objectmove(O,X,Y,S) :- object(O), xcoo(X), ycoo(Y), astep(S), spoil.
nobjectmove(O,X,Y,S) :- object(O), xcoo(X), ycoo(Y), astep(S), spoil.
objectsOkUpTo(O) :- object(O), spoil.
objectHasNoXUpTo(O,X) :- object(O), xcoo(X), step(S), spoil.
objectHasNoYUpTo(O,Y) :- object(O), ycoo(Y), step(S), spoil.

% activate this for enforcing saturation
:- not spoil.

% preset infeasible plan (object at position X=4, Y=1 cannot be seen)
%move(a,1,2,0).
%move(b,4,3,0).
%objectX(u,4,0).
%objectY(u,1,0).

